//===== Hercules Script ======================================
//= Eden Group Quests - Mission [100 - 110]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.3
//===== File Encoding ========================================
//= CP-949 (EUC-KR compatible). Do NOT read/save it as UTF-8.
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 100 - 110.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official script. [Euphy]
//============================================================

moc_para01,26,93,5	script	111-120 Mission Board	4_BOARD3,{
	if (countitem(6219) < 1) {
		mes "Wait a minute!";
		mes "-You need to have an-";
		mes "-^4d4dff'Eden Group Mark'^000000-";
		mes "-to receive these missions.-";
		next;
		switch(select("Just walk away.:^0000FFEden Group^000000 missions?:How can I be a member of the ^0000FFEden Group^000000?")) {
		case 1:
			close;
		case 2:
			mes "::The Eden Group Missions";
			mes "::are basically missions";
			mes "::requested by";
			mes "::people from all around the kingdom.";
			mes "::We just categorize them.";
			next;
			mes "::In the Eden Group,";
			mes "::we sorted out the missions";
			mes "::according to the level of difficulty.";
			mes "::According to this level,";
			mes "::you can find";
			mes "::missions that";
			mes "::suit you.";
			next;
			mes "::When you are done,";
			mes "::you can come back";
			mes "::to the mission board.";
			next;
			mes "::Then you will be";
			mes "::rewarded!!";
			mes "::Simple, isn't it!!";
			mes "::No commission required!!";
			close;
		case 3:
			mes "::You can become a member";
			mes "::of the Eden Group";
			mes "::to ^0000FFLaime Evenor^000000,";
			mes "::who is waiting on the 1st floor.";
			next;
			mes "::If you become a member,";
			mes "::you will get an";
			mes "::\"^006400Eden Group Mark^000000\".";
			mes "::Then you will be able";
			mes "::to see the mission board.";
			close;
		}
	}
	if (BaseLevel < 111 || BaseLevel > 120) {
		// Quest IDs: 65016-65035
		// Quest cooldown IDs: 65116-65135
		setarray .@names$[0],
			"Raydric","Khalitzburg","Wanderer",
			"Ancient Mimic","Death Word","Owl Baron","Bloody Page",
			"Dark Pinguicula","Nepenthes","Naga","Centipede Larva","Cornus","Mystic Horn",
			"Ancient Mummy",
			"Incubus","Succubus","Violy",
			"Venatu (Green)","Venatu (Orange)","Venatu (Blue)";
		setarray .@exp[0],
			300000,300000,300000,
			400000,400000,400000,
			280000,280000,280000,280000,280000,
			300000,
			300000,300000,300000,
			280000,280000,280000;
		setarray .@jexp[0],
			200000,200000,200000,
			300000,300000,300000,
			180000,180000,180000,180000,180000,
			200000,
			200000,200000,200000,
			180000,180000,180000;
		mes "You must be within level 111-120 to accept these missions.";
		for(set .@quest,65016; .@quest<=65033; set .@quest,.@quest+1) {
			set .@hunting, checkquest(.@quest,HUNTING);
			if (.@hunting == 0 || .@hunting == 1) {
				next;
				mes ":: You cannot proceed in";
				mes ":: ^0000FF"+.@names$[.@quest-65016]+" Hunting^000000.";
				mes ":: The registration to the mission";
				mes ":: is cancelled.";
				erasequest .@quest;
				close;
			} else if (.@hunting == 2) {
				next;
				mes ":: You added information";
				mes ":: about the mission";
				mes ":: ^0000FF"+.@names$[.@quest-65016]+" Hunting^000000";
				mes ":: on the mission board.";
				mes ":: You've got a";
				mes ":: salutary experience.";
				erasequest .@quest;
				getexp .@exp[.@quest-65016],.@jexp[.@quest-65016];
				close;
			}
		}
		close;
	}
	mes "[111-120 Mission Board]";
	mes "There are several requests on this board.";
	next;
	switch(select("^8B4513Glast Heim^000000:^8B4513Thanatos Tower^000000:^8B4513Ash Vacuum^000000:^8B4513Pyramid^000000:^8B4513Geffenia^000000:^8B4513Juperos^000000")) {
	case 1:
		mes "^8B4513Glast Heim^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFRaydric^000000:^0000FFKhalitzburg^000000:^0000FFWanderer^000000")) {
			case 1: callsub L_Quest,65016,"Raydric",30,300000,200000;
			case 2: callsub L_Quest,65017,"Khalitzburg",30,300000,200000;
			case 3: callsub L_Quest,65018,"Wanderer",30,300000,200000;
		}
	case 2:
		mes "^8B4513Thanatos Tower^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFAncient Mimic^000000:^0000FFDeath Word^000000:^0000FFOwl Baron^000000")) {
			case 1: callsub L_Quest,65019,"Ancient Mimic",30,400000,300000;
			case 2: callsub L_Quest,65020,"Death Word",30,400000,300000;
			case 3: callsub L_Quest,65021,"Owl Baron",20,400000,300000;
		}
	case 3:
		mes "^8B4513Ash Vacuum^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFDark Pinguicula^000000:^0000FFNepenthes^000000:^0000FFNaga^000000:^0000FFCentipede Larva^000000:^0000FFCornus^000000")) {
			case 1: callsub L_Quest,65022,"Dark Pinguicula",30,280000,180000;
			case 2: callsub L_Quest,65023,"Nepenthes",30,280000,180000;
			case 3: callsub L_Quest,65024,"Naga",30,280000,180000;
			case 4: callsub L_Quest,65025,"Centipede Larva",20,280000,180000;
			case 5: callsub L_Quest,65026,"Cornus",30,280000,180000;
		}
	case 4:
		mes "^8B4513Pyramid^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFAncient Mummy^000000")) {
			case 1: callsub L_Quest,65027,"Ancient Mummy",30,300000,200000;
		}
	case 5:
		mes "^8B4513Geffenia^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFIncubus^000000:^0000FFSuccubus^000000:^0000FFVioly^000000")) {
			case 1: callsub L_Quest,65028,"Incubus",10,300000,200000;
			case 2: callsub L_Quest,65029,"Succubus",10,300000,200000;
			case 3: callsub L_Quest,65030,"Violy",10,300000,200000;
		}
	case 6:
		mes "^8B4513Juperos^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFVenatu (Green)^000000:^0000FFVenatu (Orange)^000000:^0000FFVenatu (Blue)^000000")) {
			case 1: callsub L_Quest,65031,"Venatu (Green)",30,280000,180000;
			case 2: callsub L_Quest,65032,"Venatu (Orange)",30,280000,180000;
			case 3: callsub L_Quest,65033,"Venatu (Blue)",30,280000,180000;
		}
	}
	end;

//callsub L_Quest,<quest ID>,"<monster name>",<target>,<reward EXP>,<reward JEXP>;
L_Quest:
	set .@quest1, getarg(0);
	set .@quest2, .@quest1+100;
	set .@playtime, checkquest(.@quest2,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[111-120 Mission Board]";
		mes "You need to wait 1 hour before you can take this mission again.";
		close;
	}
	set .@hunting, checkquest(.@quest1,HUNTING);
	if (.@hunting == 0 || .@hunting == 1) {
		mes "[111-120 Mission Board]";
		mes "Have you finished hunting all "+getarg(2)+" "+getarg(1)+" yet?";
		close;
	} else if (.@hunting == 2) {
		mes "[111-120 Mission Board]";
		mes "You have completed the hunting.";
		mes "Please accept this reward as a compensation.";
		erasequest .@quest1;
		if (.@playtime > -1) erasequest .@quest2;
		setquest .@quest2;

		getexp getarg(3),getarg(4);
		close;
	}/* else if (.@hunting == 3) {
//		recall_completequest .@quest1
		erasequest .@quest1;
		mes "[91-99 Mission Board]";
		mes "You have re-accepted the "+getarg(1)+" Hunting mission.";
		close;
	}*/ else {
		mes "[111-120 Mission Board]";
		mes "Hunt "+getarg(2)+" "+getarg(1)+" monsters and post your success on this board.";
		next;
		if(select("I'll hunt them.:No thanks.") == 1) {
			mes "[111-120 Mission Board]";
			mes "When you have completed the mission, post it on this board and collect your reward.";
			if (.@playtime > -1) erasequest .@quest2;
			setquest .@quest1;
		}
		close;
	}
}
