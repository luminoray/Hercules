//===== Hercules Script ======================================
//= Eden Group Quests - Mission [100 - 110]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.3
//===== File Encoding ========================================
//= CP-949 (EUC-KR compatible). Do NOT read/save it as UTF-8.
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 100 - 110.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official script. [Euphy]
//============================================================

moc_para01,24,93,5	script	100-110 Mission Board	4_BOARD3,{
	if (countitem(6219) < 1) {
		mes "Wait a minute!";
		mes "-You need to have an-";
		mes "-^4d4dff'Eden Group Mark'^000000-";
		mes "-to receive these missions.-";
		next;
		switch(select("Just walk away.:^0000FFEden Group^000000 missions?:How can I be a member of the ^0000FFEden Group^000000?")) {
		case 1:
			close;
		case 2:
			mes "::The Eden Group Missions";
			mes "::are basically missions";
			mes "::requested by";
			mes "::people from all around the kingdom.";
			mes "::We just categorize them.";
			next;
			mes "::In the Eden Group,";
			mes "::we sorted out the missions";
			mes "::according to the level of difficulty.";
			mes "::According to this level,";
			mes "::you can find";
			mes "::missions that";
			mes "::suit you.";
			next;
			mes "::When you are done,";
			mes "::you can come back";
			mes "::to the mission board.";
			next;
			mes "::Then you will be";
			mes "::rewarded!!";
			mes "::Simple, isn't it!!";
			mes "::No commission required!!";
			close;
		case 3:
			mes "::You can become a member";
			mes "::of the Eden Group";
			mes "::to ^0000FFLaime Evenor^000000,";
			mes "::who is waiting on the 1st floor.";
			next;
			mes "::If you become a member,";
			mes "::you will get an";
			mes "::\"^006400Eden Group Mark^000000\".";
			mes "::Then you will be able";
			mes "::to see the mission board.";
			close;
		}
	}
	if (BaseLevel < 100 || BaseLevel > 110) {
		// Quest IDs: 65001-65015
		// Quest cooldown IDs: 65101-65115
		setarray .@names$[0],
			"Carat","Arclouse","Anolian","Sting","Majoruros",
			"Pinguicula","Luciola Vespa",
			"Desert Wolf","Ice Titan","Snowier",
			"Nightmare Terror","Deleter",
			"Loli Ruri","Medusa","Anubis";
		setarray .@exp[0],
			154000,154000,198400,198400,252000,
			154000,198400,
			154000,198400,154000,
			154000,154000,
			198400,154000,198400;
		setarray .@jexp[0],
			148000,148000,186500,186500,252000,
			148000,186500,
			148000,186500,148000,
			148000,148000,
			186500,148000,186500;
		mes "You must be within level 100-110 to accept these missions.";
		for(set .@quest,65001; .@quest<=65015; set .@quest,.@quest+1) {
			set .@hunting, checkquest(.@quest,HUNTING);
			if (.@hunting == 0 || .@hunting == 1) {
				next;
				mes ":: You cannot proceed in";
				mes ":: ^0000FF"+.@names$[.@quest-65001]+" Hunting^000000.";
				mes ":: The registration to the mission";
				mes ":: is cancelled.";
				erasequest .@quest;
				close;
			} else if (.@hunting == 2) {
				next;
				mes ":: You added information";
				mes ":: about the mission";
				mes ":: ^0000FF"+.@names$[.@quest-65001]+" Hunting^000000";
				mes ":: on the mission board.";
				mes ":: You've got a";
				mes ":: salutary experience.";
				erasequest .@quest;
				getexp .@exp[.@quest-65001],.@jexp[.@quest-65001];
				close;
			}
		}
		close;
	}
	mes "[100-110 Mission Board]";
	mes "There are several requests on this board.";
	next;
	switch(select("^8B4513Glast Heim^000000:^8B4513Ash Vacuum^000000:^8B4513Ice Dungeon^000000:^8B4513Magma Dungeon^000000:^8B4513Other regions^000000")) {
	case 1:
		mes "^8B4513Glast Heim^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFCarat^000000:^0000FFArclouse^000000:^0000FFAnolian^000000:^0000FFSting^000000:^0000FFMajoruros^000000")) {
			case 1: callsub L_Quest,65001,"Carat",30,154000,148000;
			case 2: callsub L_Quest,65002,"Arclouse",22,154000,148000;
			case 3: callsub L_Quest,65003,"Anolian",30,198400,186500;
			case 4: callsub L_Quest,65004,"Sting",30,198400,186500;
			case 5: callsub L_Quest,65005,"Majoruros",30,252000,252000;
		}
	case 2:
		mes "^8B4513Ash Vacuum^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFPinguicula^000000:^0000FFLuciola Vespa^000000")) {
			case 1: callsub L_Quest,65006,"Pinguicula",30,154000,148000;
			case 2: callsub L_Quest,65007,"Luciola Vespa",30,198400,186500;
		}
	case 3:
		mes "^8B4513Ice Dungeon^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFDesert Wolf^000000:^0000FFIce Titan^000000:^0000FFSnowier^000000")) {
			case 1: callsub L_Quest,65008,"Desert Wolf",30,154000,148000;
			case 2: callsub L_Quest,65009,"Ice Titan",30,198400,186500;
			case 3: callsub L_Quest,65010,"Snowier",30,154000,148000;
		}
	case 4:
		mes "^8B4513Magma Dungeon^000000.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFNightmare Terror^000000:^0000FFDeleter (Sky)^000000")) {
			case 1: callsub L_Quest,65011,"Nightmare Terror",30,154000,148000;
			case 2: callsub L_Quest,65012,"Deleter (Sky)",22,154000,148000;
		}
	case 5:
		mes "Other Regions.";
		mes "Which monster will you hunt or have you finished hunting?";
		next;
		switch(select("^0000FFLoli Ruri^000000:^0000FFMedusa^000000:^0000FFAnubis^000000")) {
			case 1: callsub L_Quest,65013,"Loli Ruri",30,198400,186500;
			case 2: callsub L_Quest,65014,"Medusa",30,154000,148000;
			case 3: callsub L_Quest,65015,"Anubis",20,198400,186500;
		}
	}
	end;

//callsub L_Quest,<quest ID>,"<monster name>",<target>,<reward EXP>,<reward JEXP>;
L_Quest:
	set .@quest1, getarg(0);
	set .@quest2, .@quest1+100;
	set .@playtime, checkquest(.@quest2,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[100-110 Mission Board]";
		mes "You need to wait 1 hour before you can take this mission again.";
		close;
	}
	set .@hunting, checkquest(.@quest1,HUNTING);
	if (.@hunting == 0 || .@hunting == 1) {
		mes "[100-110 Mission Board]";
		mes "Have you finished hunting all "+getarg(2)+" "+getarg(1)+" yet?";
		close;
	} else if (.@hunting == 2) {
		mes "[100-110 Mission Board]";
		mes "You have completed the hunting.";
		mes "Please accept this reward as a compensation.";
		erasequest .@quest1;
		if (.@playtime > -1) erasequest .@quest2;
		setquest .@quest2;

		getexp getarg(3),getarg(4);
		close;
	}/* else if (.@hunting == 3) {
//		recall_completequest .@quest1
		erasequest .@quest1;
		mes "[91-99 Mission Board]";
		mes "You have re-accepted the "+getarg(1)+" Hunting mission.";
		close;
	}*/ else {
		mes "[100-110 Mission Board]";
		mes "Hunt "+getarg(2)+" "+getarg(1)+" monsters and post your success on this board.";
		next;
		if(select("I'll hunt them.:No thanks.") == 1) {
			mes "[100-110 Mission Board]";
			mes "When you have completed the mission, post it on this board and collect your reward.";
			if (.@playtime > -1) erasequest .@quest2;
			setquest .@quest1;
		}
		close;
	}
}
